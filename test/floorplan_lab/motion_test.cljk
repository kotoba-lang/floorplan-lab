(ns floorplan-lab.motion-test
  (:require [clojure.test :refer [deftest is testing]]
            [floorplan-lab.motion :as motion]))

(def ^:private half-pi 1.5707963267948966)

(defn- close? [a b eps] (and (<= (- a b) eps) (<= (- b a) eps)))

(deftest heading-integrates-constant-gyro-z
  (testing "constant yaw rate for a known duration -> known total turn"
    (let [samples (repeat 100 {:gz half-pi :dt 0.01}) ; 90deg turn over 1s
          traj (motion/walk-trajectory samples)
          final (last traj)]
      (is (close? (:heading final) half-pi 1e-9)))))

(deftest trapezoidal-accel-profile-known-displacement
  (testing "accelerate/cruise/decelerate along +x for an analytically
            known total distance (trapezoidal velocity profile)"
    (let [dt 0.01
          accel-phase (repeat 100 {:ax 1.0 :dt dt})
          cruise-phase (repeat 100 {:ax 0.0 :dt dt})
          decel-phase (repeat 100 {:ax -1.0 :dt dt})
          samples (concat accel-phase cruise-phase decel-phase)
          traj (motion/walk-trajectory samples)
          final (last traj)
          ;; triangle (accel) + rectangle (cruise) + triangle (decel):
          ;; 0.5*1*1 + 1.0*1 + 0.5*1*1 = 2.0
          expected-x 2.0]
      (is (close? (:x final) expected-x 0.05))
      (is (close? (:vx final) 0.0 1e-9))
      (is (close? (:y final) 0.0 1e-9) "no lateral accel -> no y displacement"))))

(deftest walk-trajectory-includes-start-state-as-index-0
  (let [traj (motion/walk-trajectory [{:ax 1.0 :dt 0.1}])]
    (is (= 2 (count traj)))
    (is (= motion/initial-state (first traj)))))

(deftest step-defaults-missing-sample-keys-to-zero
  (is (= motion/initial-state (motion/step motion/initial-state {}))))
